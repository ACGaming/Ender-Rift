// For those who want the bleeding edge
buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT'
    }
}

plugins {
    id 'com.github.johnrengelman.shadow' version '1.2.3'
}

repositories {
    maven {
        url "http://dvs1.progwml6.com/files/maven"
    }
    maven {
        url 'http://maven.epoxide.xyz'
    }
    maven {
        url 'http://dogforce-games.com/maven'
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'

version = "1.1.0"
group= "gigaherz.enderRift"
archivesBaseName = "EnderRift-1.9.4"

minecraft {
    version = "1.9.4-12.17.0.1987"
    runDir = "run"

    mappings = "snapshot_20160624"

    replace "@VERSION@", project.version
    replaceIn "EnderRiftMod.java"

    useDepAts = true
}

jar {
    classifier = 'slim'
}

shadowJar {
    classifier = ''
    dependencies {
        include(dependency(':GraphLib:'))
    }
}

reobf {
    shadowJar { mappingType = 'SEARGE' }
}

tasks.build.dependsOn reobfShadowJar

artifacts {
    archives shadowJar
    archives sourceJar
}

dependencies {
    deobfCompile "mezz.jei:jei_1.9.4:3.6.7.216"
    deobfCompile "net.darkhax.tesla:Tesla:1.9.4-1.1.0.24"
    deobfCompile "gigaherz.graph:GraphLib:1.1.1"
}

compileJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    options.encoding = 'UTF-8'
}

processResources
{
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

idea { module.inheritOutputDirs = true }